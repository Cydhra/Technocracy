import org.ajoberstar.grgit.Grgit

buildscript {
    project.ext {
        jvm_target = '1.8'
        kotlin_version = '1.3.40'

        minecraft_version = '1.12.2'
        forge_version = '14.23.5.2815'
        forge_plugin_version = '2.3.2-SNAPSHOT'
        mappings_version = 'snapshot_20180609'
        grgit_version = '3.1.1'

        forgelin_version = '1.8.2'
        jei_version = '4.15.0.268'
        zerocore_version = '1.12-0.1.1.0'
        ctm_version = 'MC1.12.2-0.3.3.22'
        waila_version = '1.8.26-B41_1.12.2'
        top_version = '1.12-1.4.28-17'

        mod_version = '1.0'

        root_build_dir = project.buildDir
    }

    ext.mod_id = 'technocracy.monolith'

    repositories {
        mavenCentral()
        jcenter()
        maven { url = "https://files.minecraftforge.net/maven" }
        maven { url 'https://jitpack.io' }
    }

    dependencies {
        classpath group: 'org.jetbrains.kotlin', name: 'kotlin-gradle-plugin', version: kotlin_version
        classpath group: 'com.github.Minecraft-Technocracy', name: 'ForgeGradle', version: forge_plugin_version
        classpath group: 'org.ajoberstar.grgit', name: 'grgit-gradle', version: grgit_version
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'kotlin'
apply plugin: 'idea'

allprojects {
    apply plugin: 'net.minecraftforge.gradle.forge'
    apply plugin: 'kotlin'
    apply plugin: 'idea'

    version = project.mod_version
    group = "net.cydhra.technocracy"

    libsDirName = "$root_build_dir/artifacts"

    sourceSets {
        // the api source set creates more problems on compile than it solves, so we just throw it away
        remove api
    }

    repositories {
        jcenter()
        maven {
            name 'Forgelin Maven Repository'
            url 'https://maven.shadowfacts.net/'
        }
        maven {
            name "JEI Maven Repository"
            url "https://dvs1.progwml6.com/files/maven/"
        }
        maven {
            name "zerocore Repository"
            url "http://maven.zerono.it/"
        }
        maven {
            name "chisel Repository"
            url "https://maven.tterrag.com"
        }
        maven {
            name "waila Repository"
            url "http://tehnut.info/maven"
        }
        maven { // TOP
            name 'tterrag maven'
            url "https://maven.tterrag.com/"
        }
        maven {
            name 'jitpack'
            url 'https://jitpack.io'
        }
    }

    minecraft {
        version = "$project.minecraft_version-$project.forge_version"
        runDir = "run"
        mappings = project.mappings_version
        makeObfSourceJar = false
    }

    processResources {
        // this will ensure that this task is redone when the versions change.
        inputs.property "version", project.version
        inputs.property "mcversion", project.minecraft.version

        // replace stuff in mcmod.info, nothing else
        from(sourceSets.main.resources.srcDirs) {
            include 'mcmod.info'

            // replace version and mcversion
            expand 'version': project.version, 'mcversion': project.minecraft.version, 'modid': project.mod_id
        }

        // copy everything else except the mcmod.info
        from(sourceSets.main.resources.srcDirs) {
            exclude 'mcmod.info'
        }

        rename '(.+_at.cfg)', 'META-INF/$1'
    }

    compileKotlin {
        kotlinOptions.jvmTarget = project.jvm_target
    }

    idea {
        module {
            inheritOutputDirs = false
            outputDir = file("out")
        }
    }
}

project('technocracy.foundation') {
    dependencies {
        deobfCompile group: 'net.shadowfacts', name: 'Forgelin', version: project.forgelin_version
        deobfCompile group: 'mezz.jei', name: "jei_$project.minecraft_version", version: project.jei_version, classifier: 'api'
        deobfCompile group: "it.zerono.mods.zerocore", name: "zerocore", version: project.zerocore_version
        deobfCompile group: "team.chisel.ctm", name: "CTM", version: project.ctm_version
        deobfCompile group: "mcp.mobius.waila", name: "Hwyla", version: project.waila_version, classifier: 'api'
        deobfCompile group: "mcjty.theoneprobe", name: "TheOneProbe-1.12", version: project.top_version

        implementation group: 'com.github.Minecraft-Technocracy', name: 'technoblock', version: '533e16d272'
    }

    jar {
        manifest {
            attributes 'FMLAT': 'technocracy_at.cfg'
        }
    }
}

project('technocracy.astronautics') {
    dependencies {
        compile project(':technocracy.foundation')
    }

    // jar configuration
    jar {
        manifest {
            attributes 'FMLAT': 'astronautics_at.cfg'
        }
    }
}

project('technocracy.optics') {
    dependencies {
        compile project(':technocracy.foundation')
    }
}

/*
 * MONOLITH CONFIGURATION
 */
dependencies {
    compile project('technocracy.foundation')
    compile project('technocracy.astronautics')
    compile project('technocracy.optics')
}

/*
 * A setup task to setup all modules at once
 */
task setup() {
    setGroup("setup")

    dependsOn('technocracy.foundation:setupDecompWorkspace',
            'technocracy.foundation:setupDevWorkspace',
            'technocracy.astronautics:setupDecompWorkspace',
            'technocracy.astronautics:setupDevWorkspace',
            ':setupDecompWorkspace')
}

task cloneTechnoblock() {
    setGroup("environment")
    def destination = "$projectDir/libs/technoblock"

    doLast {
        if (!file(destination).exists()) {
            Grgit.clone(dir: destination, uri: "https://github.com/Minecraft-Technocracy/Technoblock")
        }
    }
}